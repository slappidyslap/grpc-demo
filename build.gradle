buildscript {
    ext {
        protobufVersion = '3.23.4'
        protobufPluginVersion = '0.9.4'
        grpcVersion = '1.51.0'
        springBootVersion = '2.7.15'
        dependencyManagementVersion = '1.0.15.RELEASE'
        mapstructVersion = '1.5.5.Final'
    }
}

plugins {
    id 'java'
    id 'com.google.protobuf' version "$protobufPluginVersion"
    id 'org.springframework.boot' version "$springBootVersion" apply false
    id 'io.spring.dependency-management' version "$dependencyManagementVersion"
}


repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        dependencies {
            dependency 'net.devh:grpc-server-spring-boot-starter:2.14.0.RELEASE'
            dependencySet("io.grpc:$grpcVersion") {
                entry 'grpc-protobuf'
                entry 'grpc-stub'
            }
            dependencySet("org.mapstruct:$mapstructVersion") {
                entry 'mapstruct'
                entry 'mapstruct-processor'
            }
            dependency 'jakarta.annotation:jakarta.annotation-api:1.3.5'
            dependency 'com.google.errorprone:error_prone_annotations:2.11.0'
            dependency "com.google.protobuf:protobuf-java:$protobufVersion"
        }
    }

    group = 'kg.musabaev'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = '17'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

tasks.register('cleanAll') { t ->
    allprojects.forEach { p ->
        try {
            def task = p.tasks.named('clean').get()
            t.dependsOn task
        } catch (ignored) {
        }
    }
}
